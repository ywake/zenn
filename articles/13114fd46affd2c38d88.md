---
title: "assemblyでHelloWorld! (Mac)"
emoji: "🙋‍♂️"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["assembly"]
published: true
---

# この記事
* [42tokyo](https://42tokyo.jp/)の課題でアセンブリによるプログラミングをする機会があったので、そのチュートリアルとして`Hello, world!`するところまでを記事にしようと思います。初学者なので勘違い等が含まれるかもしれません。
* アセンブリ単体を扱った解説記事は多くあるので基礎的なことはそちらにまかせ、実際に書いて動かす過程での疑問点を調べ、纏めていきます。
* HelloWorldする系の記事も沢山あるんですが、それぞれに疑問が残る部分があるのでこの記事ではそこに焦点を当てます。

# 基本戦略
* MacOS x64 Intel 構文
* `nasm`でコンパイルし、`ld`でリンクします
* `printf`ではなく`write`で標準出力します

# 準備
1. `nasm`のインストール
	```bash
	$> brew install nasm
	```
2. `vim hello.s`

# 書く
## dataセクションを書く
[各セクションについてはこちらを参照](https://www.finddevguides.com/Assembly-programming-assembly-basic-syntax)
今回は`Hello, world!`という固定値があるのでそれを書きます。
ついでにその文字列長も用意しておきます。
```asm6502:hello.s
section .data
	msg db 'Hello, world!', 0x0a
	len equ $ - msg
```
`msg`と`len`という変数を定義しました。

#### 疑問点
この時点で色々浮かぶ疑問を一つずつ解決していきます！

1. **`db` is なに？**
	* そもそも変数を作ってストレージを割り当てる際には以下の構文が必要となります。
		```asm6502
		<変数名> <define-directive> <初期値> [, <初期値>...]
		```
	* `db`とは上の`<define-directive>`に指定できる物の一つで、1byteのストレージを割り当ててくれるようです。

	[参照](https://www.finddevguides.com/Assembly-programming-assembly-variables)

1. **1byteに`Hello, world!`は入らなくない？**
	* 1byte割り当てを1文字ずつ、連続して何度も行っているようです。(ASCIIコードに変換しながら)
	* ここは以下のようにも書き換えることができます。
		```asm6502
		msg db "H","e",'l','l','o'," world !",0x0a
		```
1. **`0x0a` is なに？**
	* 上の話と合わせて考えると簡単です。
		`0x0a`はASCIIコードの**改行**です！
1. **`equ` is なに？**
	* `equ`ディレクティブは定数の定義に使用されます。
		```asm6502:例
		num equ 42
		```
	[参照](https://www.finddevguides.com/Assembly-programming-assembly-constants)
1. **`$ - msg` is なに？**
	* これは単純に引き算をしています。
	* `$`というのは「ロケーションカウンタ」の現在値が入っていて、これが今は`msg`の直後を示しています。その数値から`msg`（文字列の先頭）の数値を引くと`msg`の文字列長が計算できます。
	* 説明のために、C言語で同じ考え方を使って`strlen()`を書くとこんな感じ
		```c
		int my_strlen(char *str)
		{
			char *dollar = str;

			while (*dollar != '\x00')
				dollar++;
			return ((int)(dollar - str)); // ポインタの差が文字列の長さ
		}
		```
	[参照](https://www.finddevguides.com/Assembly-programming-assembly-strings)

これでdataセクションの記述はすべて合点が行きました！

## textセクションを書く
システムコールの「write」を呼び出し、`Hello, world!`を標準出力します。
この辺のasmの解説は他サイトに溢れてるのでサラッとやります。
[システムコール一覧](https://opensource.apple.com/source/xnu/xnu-1504.3.12/bsd/kern/syscalls.master)

```asm6502:hello.s
section .data
	msg db 'Hello, world!', 0x0a
	len equ $ - msg

section .text
	global _main		; '_main'を外部参照可能に
_main:
	; SYSCALL: write(1, msg, len);
	mov rax, 0x2000004	; MacOSにおけるwrite関数の番号
	mov rdi, 1		; write()の第1引数。標準出力へ
	mov rsi, msg		; write()第2引数
	mov rdx, len		; write()第3引数
	syscall

	; SYSCALL: exit(0)
	mov rax, 0x2000001	; exit関数の番号
	mov rdi, 0		; 正常終了
	syscall
```

#### 疑問点
1. **なぜ`_main:`？ `_start:` で解説するサイトもあるけど…**
	* ここはエントリーポイントと呼ばれ、プログラムの最初に実行する場所を指定します。名前も決まっているんですが、作る実行ファイルの形式[^1]によって変わるみたいです。（最近のMacは`_main:`ですが、昔は`start:`だったとか。。。）
	* 間違っている時は`ld`をしたときに「`_main:`が無い」とエラーが出るのでそれに合わせて書き換えるのが確実だと思います。
	* もしくは`ld -e _start -o ...`とすることでエントリーポイントを指定出来ます。

1. **`syscall`の部分を`int 0x80`で解説するサイトもあるけど…**
	* `int`というのは割り込みシグナルを送る命令で、その際`0x80`番を指定するとシステムコールが発行されます。
	* つまりどちらも同じシステムコールの呼び出しで、`int 0x80`はx86とx86_64両方で使えて、`syscall`はx86_64でのみ使えます。今回はどちらでも構わないです。
	* 違いは下の項目

	[参照](https://en.wikibooks.org/wiki/X86_Assembly/Interfacing_with_Linux#Making_a_system_call)
1. **引数を入れるためのレジスタがサイトによって違う！**
	* `int 0x80`を使ったときと`syscall`を使ったときで、引数のために使われるレジスタが変わります。

	| call | call番号 | 第1引数 | 第2 | 第3 | 第4 | 第5 | 第6 | 返り値 |
	|-|-|-|-|-|-|-|-|-|-|
	| `int 0x80` | eax | ebx | ecx | edx | esi | edi | ebp | eax |
	| `syscall`  | rax | rdi | rsi | rdx | r10 |  r8 |  r9 | rax |

	[参照](https://en.wikibooks.org/wiki/X86_Assembly/Interfacing_with_Linux#Via_interrupt)
1. **Macはシステムコールの番号に`0x2000000`が足されているのはなぜ？**
	* UNIXのシステムコールと区別するためにそうされてるみたいです。探求してる方が[こちら](http://dustin.schultz.io/mac-os-x-64-bit-assembly-system-calls.html)にいました。
	* [定義](https://opensource.apple.com/source/xnu/xnu-792.13.8/osfmk/mach/i386/syscall_sw.h)をみると2だけでなく1~4までそれぞれ意味がありそう。


# コンパイル・実行する
```bash
$> nasm -f macho64 -o hello.o hello.s
$> ld -o hello hello.o -lSystem
$> ./hello
Hello, world!
```
* `nasm -f format`で実行ファイルの形式[^1]を指定します。
* `ld`でリンクする時`libSystem.dylib`を一緒にリンクします。
	- `libSystem.dylib`の中にある`libc`が必要なだけなので、代わりに`-lc`としても出来ます。
		> なぜ`libc`が必要なのかはわかりませんでしたが、`libc`の中には`crt0.o`というスタートアップルーチン^[C言語で`main()`の前に動くもの]が入っています。
		> これには`_start:`ラベルが含まれているのでmacが`_main:`を指定するのと合わせたら、この辺が理由なのかなぁ…と予想しています。予想なので参考にはしないで下さい。

[^1]: elf32, elf64, Mach-O 64bit, ...
